bin2chen

medium

# _updateVirtualPrice() virtualPrice smaller than expected

## Summary
_updateVirtualPrice() passes the wrong “lastUpdateTime” causing the “virtualPrice” to be smaller than expected

## Vulnerability Detail

In Vault_Velo#_updateVirtualPrice()
Calculate the price virtualPrice using threeMinuteDelta  * interestPer3Min
But updating lastUpdateTime using lastUpdateTime = _currentBlockTime (should be using lastUpdateTime + (threeMinuteDelta*THREE_MIN)) causes virtualPrice to become smaller

Here is the example:
Suppose the current :  lastUpdateTime = 00:00 , virtualPrice = 1 , interestPer3Min = 1.5,
when the time comes to 00:05, then the current calculation:
threeMinuteDelta = (00:05 - 00:00) / 180 = 1    (round down)
virtualPrice = virtualPrice * interestPer3Min = 1 * 1.5 =  1.5
lastUpdateTime = _currentBlockTime = 00:05      (it is wrong,  should be 00:03)

When the time comes 00:06 
threeMinuteDelta = (00:06 - 00:05) / 180 = 0
because threeMinuteDelta ==0 so resulting virtualPrice remains the same, still equal to 1.5
but The normal value should be: 00:00 to 00:06, have two "interestPer3Min", should be 1 * 1.5 * 1.5 = 2.25
virtualPrice becomes smaller than expected

```solidity
    function _updateVirtualPrice(uint256 _currentBlockTime, address _collateralAddress) internal { 
        (   ,
            ,
            ,
            uint256 interestPer3Min,
            uint256 lastUpdateTime,
            uint256 virtualPrice
        ) = _getCollateral(_collateralAddress);
        uint256 timeDelta = _currentBlockTime - lastUpdateTime;
        //exit gracefully if two users call the function for the same collateral in the same block 
        uint256 threeMinuteDelta = timeDelta / 180; 
        if(threeMinuteDelta > 0) {
            for (uint256 i = 0; i < threeMinuteDelta; i++ ){
            virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE; 
            }
            //***@audit error pass _currentBlockTime , should pass lastUpdateTime + (threeMinuteDelta*180)  ***/ 
            collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, _currentBlockTime);
        }
    }

```
note:
Vault_Base_ERC20#_updateVirtualPrice() has same problem

CollateralBook#updateVirtualPriceSlowly() is right


   
## Impact
virtualPrice becomes smaller
## Code Snippet
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Velo.sol#L259-L263
## Tool used

Manual Review

## Recommendation

```solidity
    function _updateVirtualPrice(uint256 _currentBlockTime, address _collateralAddress) internal { 
        (   ,
            ,
            ,
            uint256 interestPer3Min,
            uint256 lastUpdateTime,
            uint256 virtualPrice
        ) = _getCollateral(_collateralAddress);
        uint256 timeDelta = _currentBlockTime - lastUpdateTime;
        //exit gracefully if two users call the function for the same collateral in the same block 
        uint256 threeMinuteDelta = timeDelta / 180; 
        if(threeMinuteDelta > 0) {
            for (uint256 i = 0; i < threeMinuteDelta; i++ ){
            virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE; 
            }
-           collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, _currentBlockTime);
+           collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, lastUpdateTime + (threeMinuteDelta * 180));
        }
    }
```