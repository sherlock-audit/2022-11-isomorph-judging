KingNFT

high

# The ````ONE_HUNDRED_DOLLARS```` minimum loan security limit can be easily bypassed

## Summary
The  ````ONE_HUNDRED_DOLLARS```` minimum loan security limit is only applied to ````openLoan()```` function but missing on ````closeLoan()```` function. An attacker can combine a loan with a  immediately close to bypass it.

Example:
```solidity
loan 100 isoUSD, then immediately close 99 isoUSD, remainning 1 isoUSD debt
```

## Vulnerability Detail
See summary

## Impact
The minimum loan security protection doesn't work.

## Code Snippet
```solidity
function openLoan(
    address _collateralAddress,
    uint256 _colAmount,
    uint256 _USDborrowed
    ) external override whenNotPaused 
    {
    _collateralExists(_collateralAddress);
    IERC20 collateral = IERC20(_collateralAddress);
    require(collateral.balanceOf(msg.sender) >= _colAmount, "User lacks collateral quantity!");
    //make sure virtual price is related to current time before fetching collateral details
    //slither-disable-next-line reentrancy-vulnerabilities-1
    _updateVirtualPrice(block.timestamp, _collateralAddress);  
    
    (   
        bytes32 currencyKey,
        uint256 minOpeningMargin,
        ,
        ,
        ,
        uint256 virtualPrice,
        
    ) = _getCollateral(_collateralAddress);
    //check for frozen or paused collateral
    _checkIfCollateralIsActive(currencyKey);

    //make sure the total isoUSD borrowed doesn't exceed the opening borrow margin ratio
    uint256 colInUSD = priceCollateralToUSD(currencyKey, _colAmount + collateralPosted[_collateralAddress][msg.sender]);
    uint256 totalUSDborrowed = _USDborrowed +  (isoUSDLoaned[_collateralAddress][msg.sender] * virtualPrice)/LOAN_SCALE;
    require(totalUSDborrowed >= ONE_HUNDRED_DOLLARS, "Loan Requested too small");  // @audit bypassed
    uint256 borrowMargin = (totalUSDborrowed * minOpeningMargin) / LOAN_SCALE;
    require(colInUSD >= borrowMargin, "Minimum margin not met!");

    //update mappings with new loan amounts
    collateralPosted[_collateralAddress][msg.sender] = collateralPosted[_collateralAddress][msg.sender] + _colAmount;
    isoUSDLoaned[_collateralAddress][msg.sender] = isoUSDLoaned[_collateralAddress][msg.sender] + _USDborrowed;
    isoUSDLoanAndInterest[_collateralAddress][msg.sender] = isoUSDLoanAndInterest[_collateralAddress][msg.sender] + ((_USDborrowed * LOAN_SCALE) / virtualPrice);
    
    emit OpenOrIncreaseLoan(msg.sender, _USDborrowed, currencyKey, _colAmount);

    //Now all effects are handled, transfer the assets so we follow CEI pattern
    _increaseCollateral(collateral, _colAmount); // @audit reentrancy attack
    _increaseLoan(_USDborrowed);
    
    
}


function closeLoan(
    address _collateralAddress,
    uint256 _collateralToUser,
    uint256 _USDToVault
    ) external override whenNotPaused 
    {
    _collateralExists(_collateralAddress);
    _closeLoanChecks(_collateralAddress, _collateralToUser, _USDToVault);
    //make sure virtual price is related to current time before fetching collateral details
    //slither-disable-next-line reentrancy-vulnerabilities-1
    _updateVirtualPrice(block.timestamp, _collateralAddress);
    (   
        bytes32 currencyKey,
        uint256 minOpeningMargin,
        ,
        ,
        ,
        uint256 virtualPrice,
        
    ) = _getCollateral(_collateralAddress);
    //check for frozen or paused collateral
    _checkIfCollateralIsActive(currencyKey);
    uint256 isoUSDdebt = (isoUSDLoanAndInterest[_collateralAddress][msg.sender] * virtualPrice) / LOAN_SCALE;
    require( isoUSDdebt >= _USDToVault, "Trying to return more isoUSD than borrowed!");
    uint256 outstandingisoUSD = isoUSDdebt - _USDToVault;
    if(outstandingisoUSD >= TENTH_OF_CENT){ //ignore leftover debts less than $0.001
        uint256 collateralLeft = collateralPosted[_collateralAddress][msg.sender] - _collateralToUser;
        uint256 colInUSD = priceCollateralToUSD(currencyKey, collateralLeft); 
        uint256 borrowMargin = (outstandingisoUSD * minOpeningMargin) / LOAN_SCALE;
        require(colInUSD > borrowMargin , "Remaining debt fails to meet minimum margin!");
    }
    
    //record paying off loan principle before interest
    //slither-disable-next-line uninitialized-local-variables
    uint256 interestPaid;
    uint256 loanPrinciple = isoUSDLoaned[_collateralAddress][msg.sender];
    if( loanPrinciple >= _USDToVault){
        //pay off loan principle first
        isoUSDLoaned[_collateralAddress][msg.sender] = loanPrinciple - _USDToVault;
    }
    else{
        interestPaid = _USDToVault - loanPrinciple;
        //loan principle is fully repaid so record this.
        isoUSDLoaned[_collateralAddress][msg.sender] = 0;
    }
    //update mappings with reduced amounts
    isoUSDLoanAndInterest[_collateralAddress][msg.sender] = isoUSDLoanAndInterest[_collateralAddress][msg.sender] - ((_USDToVault * LOAN_SCALE) / virtualPrice);
    collateralPosted[_collateralAddress][msg.sender] = collateralPosted[_collateralAddress][msg.sender] - _collateralToUser;
    emit ClosedLoan(msg.sender, _USDToVault, currencyKey, _collateralToUser);
    //Now all effects are handled, transfer the assets so we follow CEI pattern
    _decreaseLoan(_collateralAddress, _collateralToUser, _USDToVault, interestPaid);
}
```
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Synths.sol#L197
## Tool used

Manual Review

## Recommendation
```diff
function closeLoan(
   // ...
    ) external override whenNotPaused 
    {
    // ...
    uint256 outstandingisoUSD = isoUSDdebt - _USDToVault;
    if(outstandingisoUSD >= TENTH_OF_CENT){ //ignore leftover debts less than $0.001
+       require(outstandingisoUSD >= ONE_HUNDRED_DOLLARS, "Remaining debt too small");
        uint256 collateralLeft = collateralPosted[_collateralAddress][msg.sender] - _collateralToUser;
        uint256 colInUSD = priceCollateralToUSD(currencyKey, collateralLeft); 
        uint256 borrowMargin = (outstandingisoUSD * minOpeningMargin) / LOAN_SCALE;
        require(colInUSD > borrowMargin , "Remaining debt fails to meet minimum margin!");
    }

    // ...
}

function increaseCollateralAmount(
    address _collateralAddress,
    uint256 _colAmount
    ) external override whenNotPaused 
    {
    // ...
    uint256 totalCollat = collateralPosted[_collateralAddress][msg.sender] + _colAmount;
    uint256 colInUSD = priceCollateralToUSD(currencyKey, totalCollat);
    uint256 USDborrowed = (isoUSDLoanAndInterest[_collateralAddress][msg.sender] * virtualPrice) / LOAN_SCALE;
+   require(USDborrowed >= ONE_HUNDRED_DOLLARS, "USD borrowed too small");    
    // ...
}
```
