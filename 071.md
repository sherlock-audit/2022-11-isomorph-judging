KingNFT

high

# The ````virtualPrice```` is counted less than expected

## Summary
The ````_updateVirtualPrice()```` function, in both Vault_Base_ERC20.sol and Vault_Velo.sol, is not correctly implemented, cause ````virtualPrice```` of collateral is counted less than expected.

## Vulnerability Detail
The vulnerability point
```solidity
function _updateVirtualPrice(uint256 _currentBlockTime, address _collateralAddress) internal { 
    (   ,
        ,
        ,
        uint256 interestPer3Min,
        uint256 lastUpdateTime,
        uint256 virtualPrice,

    ) = _getCollateral(_collateralAddress);
    uint256 timeDelta = _currentBlockTime - lastUpdateTime;

    uint256 threeMinuteDelta = timeDelta / 180; 
    if(threeMinuteDelta > 0) {
        for (uint256 i = 0; i < threeMinuteDelta; i++ ){
        virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE; 
        }
        collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice,
           _currentBlockTime); // @audit '_currentBlockTime' should be 'lastUpdateTime + threeMinuteDelta * 180'
    }
}
```

__An example to illustrate this__
Let's say, the initial parameters are
```solidity
lastUpdateTime = 0
interestPer3Min = 1.1e18
virtualPrice = 1e18
```

300 seconds past, 1st update
```solidity
threeMinuteDelta = (_currentBlockTime - lastUpdateTime) / 180 = (300 - 0) / 180 = 1
virtualPrice = 1e18 * 1.1e18 / 1e18 = 1.1e18
lastUpdateTime  = 300
```

another 300 seconds past, 2nd update
```solidity
threeMinuteDelta = (_currentBlockTime - lastUpdateTime) / 180 = (600 - 300) / 180 = 1
virtualPrice = 1.1e18 * 1.1e18 / 1e18 = 1.21e18
lastUpdateTime  = 600
```

But expected virtualPrice should be
```solidity
totalTimePast = 600
threeMinuteDelta  = totalTimePast / 180 = 600 / 180 = 3
virtualPrice = 1e18 * 1.1 * 1.1 * 1.1 = 1.331e18
```


## Impact
The protocol collects less interest from users than expected.

## Code Snippet
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Base_ERC20.sol#L203
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Velo.sol#L248

## Tool used

Manual Review

## Recommendation
```diff
function _updateVirtualPrice(uint256 _currentBlockTime, address _collateralAddress) internal { 
   // ...
    if(threeMinuteDelta > 0) {
        for (uint256 i = 0; i < threeMinuteDelta; i++ ){
        virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE; 
        }
         collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice,
-           _currentBlockTime); 
+           lastUpdateTime + threeMinuteDelta * 180); 
    }
}
```