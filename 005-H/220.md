Jeiwan

high

# Debt miscalculation applies an increased interest rate to debts in the Synth vault

## Summary
Debt miscalculation applies an increased interest rate to debts in the Synth vault
## Vulnerability Detail
The current debt of a borrower is miscalculated in the `openLoan` function of the Synth vault: the virtual price is applied to the entire principal, the interest accrued before the current loan was taken isn't subtracted ([Vault_Synths.sol#L120](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Synths.sol#L120)):
```solidity
uint256 totalUSDborrowed = _USDborrowed +  (isoUSDLoaned[_collateralAddress][msg.sender] * virtualPrice)/LOAN_SCALE;
```

The `isoUSDLoaned` mapping stores borrowed amounts, it doesn't adjust them for the interest that has already been accrued. Adjusted principals are stored in the `isoUSDLoanAndInterest` variable ([Vault_Synths.sol#L127-L128](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Synths.sol#L127-L128)):
```solidity
isoUSDLoaned[_collateralAddress][msg.sender] = isoUSDLoaned[_collateralAddress][msg.sender] + _USDborrowed;
isoUSDLoanAndInterest[_collateralAddress][msg.sender] = isoUSDLoanAndInterest[_collateralAddress][msg.sender] + ((_USDborrowed * LOAN_SCALE) / virtualPrice);
```

This basically inflates the interest rate of a debt making it look like it was taken at the addition of a collateral, when the virtual price of a collateral was 1e18 ([CollateralBook.sol#L309-L317](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/CollateralBook.sol#L309-L317)) and applying the entire historical interest to it.

The other vaults are not vulnerable: [Vault_Lyra.sol#L129](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Lyra.sol#L129), [Vault_Velo.sol#L417](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Velo.sol#L417).

## Impact
Borrowers will be forced to provide more collateral than expected when they try to borrow more from the Synth. The more time has passed since the addition of a collateral to the CollateralBook contract by an admin/owner, the higher the penalty will be for the borrowers of the Synth vault.

## Code Snippet
[Vault_Synths.sol#L118-L128](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Synths.sol#L118-L128):
```solidity
//make sure the total isoUSD borrowed doesn't exceed the opening borrow margin ratio
uint256 colInUSD = priceCollateralToUSD(currencyKey, _colAmount + collateralPosted[_collateralAddress][msg.sender]);
uint256 totalUSDborrowed = _USDborrowed +  (isoUSDLoaned[_collateralAddress][msg.sender] * virtualPrice)/LOAN_SCALE;
require(totalUSDborrowed >= ONE_HUNDRED_DOLLARS, "Loan Requested too small"); 
uint256 borrowMargin = (totalUSDborrowed * minOpeningMargin) / LOAN_SCALE;
require(colInUSD >= borrowMargin, "Minimum margin not met!");

//update mappings with new loan amounts
collateralPosted[_collateralAddress][msg.sender] = collateralPosted[_collateralAddress][msg.sender] + _colAmount;
isoUSDLoaned[_collateralAddress][msg.sender] = isoUSDLoaned[_collateralAddress][msg.sender] + _USDborrowed;
isoUSDLoanAndInterest[_collateralAddress][msg.sender] = isoUSDLoanAndInterest[_collateralAddress][msg.sender] + ((_USDborrowed * LOAN_SCALE) / virtualPrice);
```

## Tool used
Manual Review

## Recommendation
Consider this change:
```diff
--- a/contracts/Isomorph/contracts/Vault_Synths.sol
+++ b/contracts/Isomorph/contracts/Vault_Synths.sol
@@ -117,7 +117,7 @@ contract Vault_Synths is Vault_Base_ERC20 {

         //make sure the total isoUSD borrowed doesn't exceed the opening borrow margin ratio
         uint256 colInUSD = priceCollateralToUSD(currencyKey, _colAmount + collateralPosted[_collateralAddress][msg.sender]);
-        uint256 totalUSDborrowed = _USDborrowed +  (isoUSDLoaned[_collateralAddress][msg.sender] * virtualPrice)/LOAN_SCALE;
+        uint256 totalUSDborrowed = _USDborrowed +  (isoUSDLoanAndInterest[_collateralAddress][msg.sender] * virtualPrice)/LOAN_SCALE;
         require(totalUSDborrowed >= ONE_HUNDRED_DOLLARS, "Loan Requested too small");
         uint256 borrowMargin = (totalUSDborrowed * minOpeningMargin) / LOAN_SCALE;
         require(colInUSD >= borrowMargin, "Minimum margin not met!");
```