Jeiwan

high

# Collateral virtual price growth can be slowed down due to rounding in virtual price calculation

## Summary
Collateral virtual price growth can be slowed down due to rounding in virtual price calculation
## Vulnerability Detail
Collateral virtual price calculation is subject to a rounding error: virtual price can be updated every 359 seconds, which will be counted as one three-minute interval due to rounding. The price will be increased only once in this period but the current block timestamp will be recorded as the last virtual price update timestamp:
- [Vault_Base_ERC20.sol#L212-L220](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Base_ERC20.sol#L212-L220):
```solidity
uint256 timeDelta = _currentBlockTime - lastUpdateTime;
//exit gracefully if two users call the function for the same collateral in the same 3min period
uint256 threeMinuteDelta = timeDelta / 180; // @audit rounds to 1 when timeDelta is 359
if(threeMinuteDelta > 0) { // @audit will be run only once when timeDelta is 359
    for (uint256 i = 0; i < threeMinuteDelta; i++ ){
    virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE;
    }
    // @audit when timeDelta is 359, current timestamp is saved even though only 180 seconds were counted
    collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, _currentBlockTime);
}
```
- [CollateralBook.sol#L231-L241](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/CollateralBook.sol#L231-L241):
```solidity
function _updateVirtualPriceAndTime(
    address _collateralAddress,
    uint256 _virtualPriceUpdate,
    uint256 _updateTime
    ) internal  {

    require( collateralProps[_collateralAddress].virtualPrice < _virtualPriceUpdate, "Incorrect virtual price" );
    require( collateralProps[_collateralAddress].lastUpdateTime < _updateTime, "Incorrect timestamp" );
    collateralProps[_collateralAddress].virtualPrice = _virtualPriceUpdate;
    collateralProps[_collateralAddress].lastUpdateTime = _updateTime;
}
```

All the vaults are vulnerable since they all update virtual prices before any operation by calling the `_updateVirtualPrice` function. Also, the `updateVirtualPriceSlowly` in the `CollateralBook` contract ([CollateralBook.sol#L258](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/CollateralBook.sol#L258)) is vulnerable as well because it implements the same virtual price calculation logic.

## Impact
A borrower can manipulate the virtual price calculation by triggering virtual price calculation in the range of 181-359 seconds after a pervious update. Due to the rounding error, the borrowing interest rate of a collateral can be reduced to benefit the manipulator.
## Code Snippet
[Vault_Base_ERC20.sol#L203](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Base_ERC20.sol#L203):
```solidity
function _updateVirtualPrice(uint256 _currentBlockTime, address _collateralAddress) internal { 
    (   ,
        ,
        ,
        uint256 interestPer3Min,
        uint256 lastUpdateTime,
        uint256 virtualPrice,

    ) = _getCollateral(_collateralAddress);
    uint256 timeDelta = _currentBlockTime - lastUpdateTime;
    //exit gracefully if two users call the function for the same collateral in the same 3min period
    uint256 threeMinuteDelta = timeDelta / 180;
    if(threeMinuteDelta > 0) {
        for (uint256 i = 0; i < threeMinuteDelta; i++ ){
        virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE; 
        }
        collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, _currentBlockTime);
    }
}
```

[CollateralBook.sol#L258-L271](https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/CollateralBook.sol#L258-L271):
```solidity
function updateVirtualPriceSlowly(
    address _collateralAddress,
    uint256 _cycles
    ) public collateralExists(_collateralAddress){ 
        Collateral memory collateral = collateralProps[_collateralAddress];
        uint256 timeDelta = block.timestamp - collateral.lastUpdateTime;
        uint256 threeMinDelta = timeDelta / THREE_MIN;

        require(_cycles <= threeMinDelta, 'Cycle count too high');
            for (uint256 i = 0; i < _cycles; i++ ){
                collateral.virtualPrice = (collateral.virtualPrice * collateral.interestPer3Min) / DIVISION_BASE; 
            }
        _updateVirtualPriceAndTime(_collateralAddress, collateral.virtualPrice, collateral.lastUpdateTime + (_cycles*THREE_MIN));
    }
```

## Tool used

Manual Review

## Recommendation
Consider using per-second interest rate to mitigate rounding errors. Alternatively, consider rounding `timeDelta` to be a multiple of 180 and updating `lastUpdateTime` to `lastUpdateTime + timeDelta`:
```diff
--- a/contracts/Isomorph/contracts/Vault_Base_ERC20.sol
+++ b/contracts/Isomorph/contracts/Vault_Base_ERC20.sol
@@ -209,14 +209,14 @@ abstract contract Vault_Base_ERC20 is RoleControl(VAULT_TIME_DELAY), Pausable {
             uint256 virtualPrice,

         ) = _getCollateral(_collateralAddress);
-        uint256 timeDelta = _currentBlockTime - lastUpdateTime;
+        uint256 timeDelta = ((_currentBlockTime - lastUpdateTime) / 180) * 180;
         //exit gracefully if two users call the function for the same collateral in the same 3min period
         uint256 threeMinuteDelta = timeDelta / 180;
         if(threeMinuteDelta > 0) {
             for (uint256 i = 0; i < threeMinuteDelta; i++ ){
             virtualPrice = (virtualPrice * interestPer3Min) / LOAN_SCALE;
             }
-            collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, _currentBlockTime);
+            collateralBook.vaultUpdateVirtualPriceAndTime(_collateralAddress, virtualPrice, lastUpdateTime + timeDelta);
         }
     }
```
This, however, will mean that virtual prices can only be updated every 180 seconds. The per-second rate approach doesn't have this drawback.