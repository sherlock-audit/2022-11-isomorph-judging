hansfriese

medium

# Liquidation ratio should meet the more strict condition to avoid overflow in the liquidation process


## Summary

The wrong liquidation ratio can cause an overflow in calculating the liquidation amount.

## Vulnerability Detail

The liquidation amount is calculated using the function `viewLiquidatableAmount` in all vaults and division is included in the calculation.

```solidity
function viewLiquidatableAmount(
    uint256 _collateralAmount,
    uint256 _collateralPrice,
    uint256 _userDebt,
    uint256 _liquidatableMargin
    ) public pure returns(uint256){
    uint256 minimumCollateralPoint = _userDebt*_liquidatableMargin;
    uint256 actualCollateralPoint = _collateralAmount*_collateralPrice;
    if(minimumCollateralPoint <=  actualCollateralPoint){
        //in this case the loan is not liquidatable at all and so we return zero
        return 0;
    }
    uint256 numerator =  minimumCollateralPoint - actualCollateralPoint;
    uint256 denominator = (_collateralPrice*LIQUIDATION_RETURN*_liquidatableMargin/LOAN_SCALE - _collateralPrice*LOAN_SCALE)/LOAN_SCALE;
    return(numerator  / denominator);

}
```

From the `liquidation_maths.pdf` provided by the protocol team, it is understood that the liquidation margin ratio and return percentage should meet $FL_m \ge 1$.

![Imgur Image](https://imgur.com/ih3sDr3.jpg)

But actually, this should be a strict inequality $FL_m \gt 1$ to prevent an overflow from division by zero.
And the collateral change function should check using a strict inequality as well.

```solidity
function queueCollateralChange(
    address _collateralAddress,
    bytes32 _currencyKey,
    uint256 _minimumRatio,
    uint256 _liquidationRatio,
    uint256 _interestPer3Min,
    uint256 _assetType,
    address _liquidityPool

) external collateralExists(_collateralAddress) onlyAdmin {
    require(_collateralAddress != address(0));
    require(_minimumRatio > _liquidationRatio);
    require(_liquidationRatio != 0);
    require(vaults[_assetType] != address(0), "Vault not deployed yet");
    IVault vault = IVault(vaults[_assetType]);
    //prevent setting liquidationRatio too low such that it would cause an overflow in callLiquidation, see appendix on liquidation maths for details.
    require( vault.LIQUIDATION_RETURN() *_liquidationRatio >= 10 ** 36, "Liquidation ratio too low");//@audit should be strictly greater

    queuedCollateralAddress = _collateralAddress;
    queuedCurrencyKey = _currencyKey;
    queuedMinimumRatio = _minimumRatio;
    queuedLiquidationRatio = _liquidationRatio;
    queuedInterestPer3Min = _interestPer3Min;
    queuedLiquidityPool = _liquidityPool;
    queuedTimestamp = block.timestamp;
}
```

I also understand that equality never holds with the current constant value `LIQUIDATION_RETURN=95 ether /100`.
But I think it is still worth pointing out because future changes might break this.

## Impact

Liquidation will revert with possibly wrong parameters.

## Code Snippet
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/CollateralBook.sol#L95
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Velo.sol#L705
https://github.com/sherlock-audit/2022-11-isomorph/blob/main/contracts/Isomorph/contracts/Vault_Base_ERC20.sol#L395

## Tool used

Manual Review

## Recommendation

Fix the liquidation ratio validation part with a strict inequality.

```solidity
    require( vault.LIQUIDATION_RETURN() *_liquidationRatio > 10 ** 36, "Liquidation ratio too low");//@audit should be strictly greater
```
